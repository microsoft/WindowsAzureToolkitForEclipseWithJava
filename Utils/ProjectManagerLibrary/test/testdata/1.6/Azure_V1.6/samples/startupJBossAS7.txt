:: *** Sample startup script containing the steps for starting JBoss Application Server 7 and deploying a WAR file. 
:: *** (Last tested with JBoss Application Server 7.0.2)

:: To use the sample, follow these steps:
:: 1) Copy all this content into approot/startup.cmd in the role folder, close this file, and edit the copy
:: 2) Place a JDK distribution as jdk.zip under approot
:: 3) Place a JBoss Application Server 7 distribution as jboss7.zip under approot in your project
::    3.1) If you want to download the server into Azure directly from a URL instead, then
::         uncomment the next line and modify the URL as appropriate:
:: cscript /NoLogo "util\download.vbs" "http://download.jboss.org/jbossas/7.0/jboss-as-7.0.2.Final/jboss-as-7.0.2.Final.zip" "jboss7.zip"

:: 4) Update SERVER_DIR_NAME below as appropriate:
::    (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET SERVER_DIR_NAME=jboss-as-7.0.2.Final

:: 5) To deploy your own WAR file, place it in approot and update WAR_NAME below:
::    (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET WAR_NAME=HelloWorld.war

:: 6) To enable the management console, add an input endpoint 9990/9990 to your Windows Azure configuration
		
			
:: **************************************************************			
:: *** Do not make changes below unless you know what you are doing.
rd "\%ROLENAME%"
mklink /D "\%ROLENAME%" "%ROLEROOT%\approot"
cd /d "\%ROLENAME%"
cscript /NoLogo util\unzip.vbs jdk.zip "%CD%"
cscript /NoLogo util\unzip.vbs jboss7.zip "%CD%"
copy "%WAR_NAME%" "%SERVER_DIR_NAME%\standalone\deployments"
cd /d "%SERVER_DIR_NAME%\bin"
set JAVA_HOME=\%ROLENAME%\jdk
set PATH=%PATH%;%JAVA_HOME%\bin
set JBOSS_Home=\%ROLENAME%\%SERVER_DIR_NAME%
start standalone.bat -b 0.0.0.0 -bmanagement 0.0.0.0

@ECHO OFF
if %ERRORLEVEL%==0 exit %ERRORLEVEL%
choice /d y /t 5 /c Y /N /M "*** Windows Azure startup failed - exiting..."
exit %ERRORLEVEL%