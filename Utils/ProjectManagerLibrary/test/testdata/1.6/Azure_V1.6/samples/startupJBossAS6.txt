:: *** Sample startup script containing the steps for starting JBoss Application Server 6 and deploying a WAR file. 
:: *** (Last tested with JBoss Application Server 6.1.0)

:: To use the sample, follow these steps:
:: 1) Copy all this content into approot/startup.cmd in the role folder, close this file, and edit the copy
:: 2) Place a JDK distribution as jdk.zip under approot
:: 3) Place a JBoss Application Server 6 distribution as jboss6.zip under approot in your project
:: 3.1) If you want to download the server into Azure directly from a URL instead, then
::          uncomment the next line and modify the URL as appropriate:
:: cscript /NoLogo "util\download.vbs" "http://download.jboss.org/jbossas/6.1/jboss-as-distribution-6.1.0.Final.zip" "jboss6.zip"

:: 4) Update SERVER_DIR_NAME below as appropriate:
:: (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET SERVER_DIR_NAME=jboss-6.1.0.Final

:: 5) To deploy your own WAR file, place it in approot and update WAR_NAME below:
:: (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET WAR_NAME=HelloWorld.war
			
			
:: *********************************************************************		
:: *** Do not make changes below unless you know what you are doing. ***
rd "\%RoleName%"
mklink /D "\%RoleName%" "%ROLEROOT%\approot"
cd /d "\%RoleName%"
cscript /NoLogo util\unzip.vbs jdk.zip "%CD%"
cscript /NoLogo util\unzip.vbs jboss6.zip "%CD%"
copy "%WAR_NAME%" "%SERVER_DIR_NAME%\server\default\deploy"
cd /d "%SERVER_DIR_NAME%\bin"
set JAVA_HOME=\%ROLENAME%\jdk
set PATH=%PATH%;%JAVA_HOME%\bin
set JBOSS_Home=\%ROLENAME%\%SERVER_DIR_NAME%
start run.bat -b 0.0.0.0

@ECHO OFF
if %ERRORLEVEL%==0 exit %ERRORLEVEL%
choice /d y /t 5 /c Y /N /M "*** Windows Azure startup failed - exiting..."
exit %ERRORLEVEL%