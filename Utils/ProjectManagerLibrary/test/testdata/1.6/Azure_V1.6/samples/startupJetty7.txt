:: *** Sample startup script containing the steps for starting Jetty 7 and deploying a WAR file. 
:: *** (Last tested with Jetty 7.4.5)

:: To use the sample, follow these steps:
:: 1) Copy all this content into approot/startup.cmd in the role folder, close this file, and edit the copy
:: 2) Place a JDK distribution as jdk.zip under approot
:: 3) Place a Jetty 7.x distribution as jetty7.zip under approot in your project
::    3.1) If you want to download the server into Azure directly from a URL instead, then
::         uncomment the next line and modify the URL as appropriate:
:: cscript /NoLogo "util\download.vbs" "http://download.eclipse.org/jetty/7.4.5.v20110725/dist/jetty-distribution-7.4.5.v20110725.zip" "jetty7.zip"

:: 4) Update SERVER_DIR_NAME below as appropriate:
::    (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET SERVER_DIR_NAME=jetty-distribution-7.4.5.v20110725

:: 5) To deploy your own WAR file, place it in approot and update WAR_NAME below:
::    (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET WAR_NAME=HelloWorld.war

			
:: **************************************************************			
:: *** Do not make changes below unless you know what you're doing.
rd "\%ROLENAME%"
mklink /D "\%ROLENAME%" "%ROLEROOT%\approot"
cd /d "\%ROLENAME%"
cscript /NoLogo util\unzip.vbs jdk.zip "%CD%"
cscript /NoLogo util\unzip.vbs jetty7.zip "%CD%"
copy %WAR_NAME% "%SERVER_DIR_NAME%\webapps\%WAR_NAME%"
cd "%SERVER_DIR_NAME%"
start \%ROLENAME%\jdk\bin\java.exe -jar start.jar

@ECHO OFF
if %ERRORLEVEL%==0 exit %ERRORLEVEL%
choice /d y /t 5 /c Y /N /M "*** Windows Azure startup failed - exiting..."
exit %ERRORLEVEL%