:: *** Sample startup script containing the steps for starting Jetty 8 and deploying a WAR file. 
:: *** (Last tested with Jetty 8.1.1)

:: To use the sample, follow these steps:
:: 1) Copy all this content into approot/startup.cmd in the role folder, close this file, and edit the copy
:: 2) Place a JDK distribution as jdk.zip under approot
:: 3) Place a Jetty 8.x distribution as jetty8.zip under approot in your project
::    3.1) If you want to download the server into Windows Azure directly from a URL instead, then
::         uncomment the next line and modify the URL as appropriate:
:: cscript /NoLogo "util\download.vbs" "http://download.eclipse.org/jetty/8.1.1.v20120215/dist/jetty-distribution-8.1.1.v20120215.zip" "jetty8.zip"

:: 4) Update SERVER_DIR_NAME below as appropriate:
::    (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET SERVER_DIR_NAME=jetty-distribution-8.1.1.v20120215

:: 5) To deploy your own WAR file, place it in approot and update WAR_NAME below:
::    (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET WAR_NAME=HelloWorld.war

			
:: *****************************************************************			
:: *** Deployment and startup logic
:: *** Do not make changes below unless you know what you are doing.

:: Deploy JDK
set JAVA_HOME=\%ROLENAME%\jdk
set PATH=%PATH%;%JAVA_HOME%\bin
cscript /NoLogo util\unzip.vbs jdk.zip "%CD%"

:: Deploy server
set JETTY_HOME=\%ROLENAME%\%SERVER_DIR_NAME%
cscript /NoLogo util\unzip.vbs jetty8.zip "\%ROLENAME%"

:: Deploy application
copy "%WAR_NAME%" "%JETTY_HOME%\webapps"

:: Start server
cd /d "%JETTY_HOME%"
start \%ROLENAME%\jdk\bin\java.exe -jar start.jar
