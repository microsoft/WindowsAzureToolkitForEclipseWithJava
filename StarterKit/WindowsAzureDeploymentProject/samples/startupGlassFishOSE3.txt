:: *** Sample startup script containing the steps for starting GlassFish Server OSE 3 and deploying a WAR file. 
:: *** (Last tested with GlassFish Server OSE 3.1.1)

:: To use the sample, follow these steps:
:: 1) Copy all this content into approot/startup.cmd in the role folder, close this file, and edit the copy
:: 2) Place a JDK distribution as jdk.zip under approot
:: 3) Place a GlassFish Server OSE 3.x distribution as glassfish3.zip under approot in your project
::    3.1) If you want to download the server into Windows Azure directly from a URL instead, then
::         uncomment the next line and modify the URL as appropriate:
:: cscript /NoLogo "util\download.vbs" "http://download.java.net/glassfish/3.1.1/release/glassfish-3.1.1.zip" "glassfish3.zip"

:: 4) Update SERVER_DIR_NAME below as appropriate:
::    (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET SERVER_DIR_NAME=glassfish3

:: 5) To deploy your own WAR file, place it in approot and update WAR_NAME below:
::    (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET WAR_NAME=HelloWorld.war

			
:: *****************************************************************			
:: *** Deployment and startup logic
:: *** Do not make changes below unless you know what you are doing.

:: Deploy JDK
set JAVA_HOME=\%ROLENAME%\jdk
set PATH=%PATH%;%JAVA_HOME%\bin
cscript /NoLogo util\unzip.vbs jdk.zip "%CD%"

:: Deploy server
set GLASSFISH_HOME=\%ROLENAME%\%SERVER_DIR_NAME%\glassfish
cscript /NoLogo util\unzip.vbs glassfish3.zip "%CD%"

:: Deploy application
copy %WAR_NAME% "%GLASSFISH_HOME%\domains\domain1\autodeploy"

:: Start server
cd /d "%GLASSFISH_HOME%\bin"
cmd /c asadmin.bat start-domain
